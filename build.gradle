buildscript {
    repositories {
        mavenCentral()
        maven {
            name = 'forge'
            url = 'http://files.minecraftforge.net/maven'
        }
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/content/repositories/snapshots/'
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'java'
apply plugin: 'forge'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

version = '1.0.0'
group = 'recipenator'

configurations {
    includeJar
}

minecraft {
    version = '1.7.10-10.13.4.1614-1.7.10'
}

repositories {
    maven {
        name = "chickenbones"
        url = "http://chickenbones.net/maven/"
    }
}

dependencies {
    compile group: 'org.luaj', name: 'luaj-jse', version: '3.0.1'
    compile 'codechicken:NotEnoughItems:1.7.10-1.0.5.120:dev'

    includeJar group: 'org.luaj', name: 'luaj-jse', version: '3.0.1'

    testCompile group: 'junit', name: 'junit', version: '4.11'
}

jar {
    from {
        configurations.includeJar.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version
    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
        // replace version and mcversion
        expand 'version': project.version, 'mcversion': project.minecraft.version
    }
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}